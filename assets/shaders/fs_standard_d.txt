#version 330 core

in vec3 aPos;
in vec3 aNormal;
in vec2 aTexCoord;

out vec4 fragColor;

uniform sampler2D diffuse_texture;
uniform vec3 viewPos;

struct Light {
  vec3 position;
  vec3 ambient;
  vec3 diffuse;
  vec3 specular;
};

uniform Light light;

struct SpotLight {
  vec3 position;
  vec3 direction;
  float cutoff;
  vec3 ambient;
  vec3 diffuse;
  vec3 specular;
};

uniform SpotLight spotlight;

struct Material {
  vec3 ambient;
  vec3 diffuse;
  vec3 specular;
  float shininess;
}; 
  
uniform Material material;

vec3 CalcPointLight(Light l, vec3 normal, vec3 fragPos, vec3 viewDir) {
    vec3 lightDir = normalize(l.position - fragPos);
    float diff = max(dot(normal, lightDir), 0.0);
    vec3 reflectDir = reflect(-lightDir, normal);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
    
    vec3 ambient = l.ambient * texture(diffuse_texture, aTexCoord).rgb;
    vec3 diffuse = l.diffuse * diff * texture(diffuse_texture, aTexCoord).rgb;
    vec3 specular = l.specular * spec * material.specular;
    return (ambient + diffuse + specular);
}

vec3 CalcSpotLight(SpotLight l, vec3 normal, vec3 fragPos, vec3 viewDir) {
    vec3 lightDir = normalize(l.position - fragPos);
    float theta = dot(lightDir, normalize(-l.direction));
    
    if(theta > l.cutoff) {       
        float diff = max(dot(normal, lightDir), 0.0);
        vec3 reflectDir = reflect(-lightDir, normal);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
        
        vec3 ambient = l.ambient * texture(diffuse_texture, aTexCoord).rgb;
        vec3 diffuse = l.diffuse * diff * texture(diffuse_texture, aTexCoord).rgb;
        vec3 specular = l.specular * spec * material.specular;
        return (ambient + diffuse + specular);
    } else {
        return vec3(0.0); 
    }
}

void main() {
  vec3 norm = normalize(aNormal);
  vec3 viewDir = normalize(viewPos - aPos);
  
  vec3 result = CalcPointLight(light, norm, aPos, viewDir);
  // Add spotlight contribution
  result += CalcSpotLight(spotlight, norm, aPos, viewDir);

  fragColor = vec4(result, 1.0);
}